#!/usr/bin/env zsh


#                            .8888b oo
#                            88   "
# .d8888b. .d8888b. 88d888b. 88aaa  dP .d8888b.
# 88'  `"" 88'  `88 88'  `88 88     88 88'  `88
# 88.  ... 88.  .88 88    88 88     88 88.  .88
# `88888P' `88888P' dP    dP dP     dP `8888P88
#                                           .88
#                                       d8888P

# Default values
name="I Sheet You Not"
bundleid="net.deanishe.alfred-i-sheet-you-not"

# Variables to delete from info.plist
unexported=(DEV)

# Path to this script's directory (i.e. the workflow root)
here="$( cd "$( dirname "$0" )"; pwd )"

# Where the .alfredworkflow file will be saved
dest="$( pwd )"

# Temporary directory to build in. Deleted afterwards.
builddir="$( mktemp -d )"

# Current workflow version
version=$( "${here}/isyn" --version 2>&1 )

# Filename of generated workflow file
fname="I-Sheet-You-Not-${version}.alfredworkflow"

# Path of generated workflow file
wfpath="${dest}/${fname}"

# Overwrite existing .alfredworkflow file
force=0

# Open the generated file in Alfred afterwards
open_after=0


# dP                dP
# 88                88
# 88d888b. .d8888b. 88 88d888b. .d8888b. 88d888b. .d8888b.
# 88'  `88 88ooood8 88 88'  `88 88ooood8 88'  `88 Y8ooooo.
# 88    88 88.  ... 88 88.  .88 88.  ... 88             88
# dP    dP `88888P' dP 88Y888P' `88888P' dP       `88888P'
#                      88
#                      dP

# Print args to STDERR
log() {
  echo "$@" >&2
}

# Help message
usage() {
  cat <<EOS
buildme [-b <ID>] [-d <DIR>] [-f] [-n <NAME>] [-o] [-r]

Build the .alfredworkflow file for this workflow.

Usage:
    buildme [-b <ID>] [-d <DIR>] [-f] [-n <NAME>] [-o] [-r]
    buildme -h

Options:
    -b <ID>     Specify a bundle ID instead of the default.
    -d <DIR>    Directory to save .alfredworkflow file in.
    -n <NAME>   The name of the workflow, e.g. "Bob's Cool Workflow"
    -r          Generate a random bundle ID.
    -f          Overwrite any existing file.
    -o          Open in Alfred after a successful build.
    -h          Show this message and exit.
EOS
}

# Generate a random bundle ID prefixed with the "proper" one
random_bundleid() {
  local pre='net.deanishe.alfred-i-sheet-you-not'
  # http://stackoverflow.com/a/19556743
  local s="$( base64 </dev/urandom | tr -dc 'a-zA-Z0-9' | head -c40 )"
  echo "${pre}.${s}"
}

# Delete build directory and (optionally) a file passed as $1
tidyup() {
  local p="$1"
  log "Deleting build directory ..."
  rm -rf "$builddir"
  [[ -f "$p" ]] && {
      log "Deleting workflow file ..."
      rm -f "$p"
  }
}

# Run a PlistBuddy command and exit the script if it fails
plbCmd() {
  local c="$1"
  /usr/libexec/PlistBuddy -c "$c" info.plist
  [[ $? -ne 0 ]] && {
    tidyup
    log "Error updating info.plist"
    exit 1
  }
}


# dP                oo dP       dP
# 88                   88       88
# 88d888b. dP    dP dP 88 .d888b88
# 88'  `88 88    88 88 88 88'  `88
# 88.  .88 88.  .88 88 88 88.  .88
# 88Y8888' `88888P' dP dP `88888P8

# Build the workflow in the repo root
build() {

  pushd "$here" 2>&1 > /dev/null

  # ---------------------------------------------------------
  # Copy workflow files into the build directory

  mkdir -p "$builddir"

  log "Copying workflow contents to build/ ..."

  rsync --recursive \
    --verbose \
    --delete-before \
    --delete-excluded \
    --perms \
    -f '- *.pyc' \
    -f '- xlrd/doc' \
    -f '- xlrd/examples' \
    -f '- *.dist-info' \
    -f '- ~$*.xlsx' \
    "./" "$builddir/"

  [[ $? -ne 0 ]] && {
    tidyup
    log "Error copying contents to build directory."
    exit 1
  }

  # ---------------------------------------------------------
  # Remove unexported variables from info.plist and
  # change bundle ID to the proper default.

  pushd "$builddir" 2>&1 > /dev/null

  log "Cleaning info.plist ..."

  plbCmd "Set :bundleid ${bundleid}"
  log "Set bundle ID: ${bundleid}"
  plbCmd "Set :name ${name}"
  log "Set workflow name: ${name}"
  plbCmd "Set :variables:DOC_PATH \"${DOC_PATH}\""
  log "Set DOC_PATH: ${DOC_PATH}"
  for v in $unexported; do
    plbCmd "Delete :variables:${v}"
  done
  # plbCmd 'Print :variables'
  # plbCmd 'Print :bundleid'

  # ---------------------------------------------------------
  # Build the .alfredworkflow file in the repo root

  log "Building ${fname} ..."

  mkdir -p "${dest}"

  [[ -f "${wfpath}" ]] && {
    rm -f "${wfpath}"
    log "Deleted existing workflow file."
  }

  # tree "$builddir"
  zip -rq8n .png:.xlsx "${wfpath}" .

  [[ $? -ne 0 ]] && {
    tidyup "${wfpath}"
    log "Error creating workflow file."
    exit 1
  }

  # ---------------------------------------------------------
  # Tidy up and go back to starting directory

  popd 2>&1 > /dev/null

  tidyup
  popd 2>&1 > /dev/null

  log "Built '${fname}' in '${dest}'"
}


#          dP oo
#          88
# .d8888b. 88 dP
# 88'  `"" 88 88
# 88.  ... 88 88
# `88888P' dP dP

while getopts ":b:d:fhn:or" opt; do
  case $opt in
    b)
      bundleid="$OPTARG"
      ;;
    d)
      dest="$OPTARG"
      ;;
    f)
      force=1
      ;;
    h)
      usage
      exit 0
      ;;
    n)
      name="$OPTARG"
      ;;
    o)
      open_after=1
      ;;
    r)
      bundleid="$( random_bundleid )"
      ;;
    \?)
      log "Invalid option: -$OPTARG"
      exit 1
      ;;
  esac
done
shift $((OPTIND-1))


wfpath="${dest}/${fname}"

[[ -f "${wfpath}" && "$force" -ne 1 ]] && {
  log "File exists: ${wfpath}"
  exit 1
}

log "building workflow '${name}' version ${version} ..."
build

[[ $open_after -eq 1 ]] && {
  open "${wfpath}"
}
